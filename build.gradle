plugins {
    id 'java-library'
    id 'checkstyle'
    id "com.github.spotbugs" version "1.6.5"
    id 'com.bmuschko.nexus' version '2.3.1'
    id "io.codearte.nexus-staging" version "0.8.0"  // logs into Sonotype OSS and does a "Close" and "Release"
}

group = 'com.imsweb'
version = '02.05.50.2'
description = 'Collaborative Stage algorithm for the java-staging-client'

// UTF-8 for all compilation tasks
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// fail the build if there are compiler warnings
tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:all" << "-Xlint:-processing" << "-Xlint:-serial" << "-Werror"
}

repositories {
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    api 'com.imsweb:staging-client-java:4.5'

    testImplementation 'junit:junit:4.12'
    testImplementation 'com.squareup.retrofit2:retrofit:2.4.0'
    testImplementation 'com.squareup.retrofit2:converter-jackson:2.4.0'
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': version,
                'Implementation-Vendor': group,
                'Created-By': System.properties['java.vm.version'] + ' (' + System.properties['java.vm.vendor'] + ')',
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Automatic-Module-Name': 'com.imsweb.staging.cs'
        )
    }
}

test {
    maxHeapSize = "1g"
}

checkstyle {
    configFile = file('config/checkstyle/checkstyle.xml')
}

spotbugs {
    toolVersion = '3.1.11'
    excludeFilter = file('config/spotbugs/spotbugs-exclude.xml')
}

// don't try to release a snapshot to a non-snapshot repository, that won't work anyway
if (version.endsWith('-SNAPSHOT'))
    gradle.startParameter.excludedTaskNames += 'closeAndReleaseRepository'

wrapper {
    gradleVersion = '5.1.1'
    distributionType = Wrapper.DistributionType.ALL
}

// run the full CS integration test suite
task csIntegrationTest(type: JavaExec) {
    main = 'com.imsweb.staging.cs.CsIntegrationTest'
    classpath = sourceSets.test.runtimeClasspath
}

modifyPom {
    project {
        name 'CS Staging Algorithm'
        description 'Collaborative Staging algorithm for the staging-client-java'
        url 'https://github.com/imsweb/staging-algorithm-cs'
        inceptionYear '2015'

        scm {
            url 'https://github.com/imsweb/staging-algorithm-cs'
            connection 'scm:https://github.com/imsweb/staging-algorithm-cs.git'
            developerConnection 'scm:git@github.com:imsweb/staging-algorithm-cs.git'
        }

        licenses {
            license {
                name 'A modified BSD License (BSD)'
                url 'https://github.com/imsweb/staging-algorithm-cs/blob/master/LICENSE'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'ctmay4'
                name 'Chuck May'
                email 'mayc@imsweb.com'
            }
        }
    }
}
