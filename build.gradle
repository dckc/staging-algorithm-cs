plugins {
    id 'java-library'
    id 'checkstyle'
    id "com.github.spotbugs" version "4.2.4"
    id 'maven-publish'
    id 'signing'
    id "io.codearte.nexus-staging" version "0.21.2"  // logs into Sonotype OSS and does a "Close" and "Release"
    id 'com.adarshr.test-logger' version '2.0.0'
    id "com.github.ben-manes.versions" version "0.28.0"
    id "org.owasp.dependencycheck" version "5.3.2.1"
    id "net.ossindex.audit" version "0.4.11"
}

group = 'com.imsweb'
version = '02.05.50.4-SNAPSHOT'
description = 'Collaborative Stage algorithm for the java-staging-client'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:all" << "-Xlint:-serial" << "-Werror"
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    spotbugs 'com.github.spotbugs:spotbugs:4.0.3'

    api 'com.imsweb:staging-client-java:4.7'

    testImplementation 'junit:junit:4.13'
    testImplementation 'com.squareup.retrofit2:retrofit:2.9.0'
    testImplementation 'com.squareup.retrofit2:converter-jackson:2.9.0'
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': archiveVersion,
                'Implementation-Vendor': group,
                'Created-By': System.properties['java.vm.version'] + ' (' + System.properties['java.vm.vendor'] + ')',
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Automatic-Module-Name': 'com.imsweb.staging.cs'
        )
    }
}

test {
    maxHeapSize = "1g"
}

checkstyle {
    toolVersion = '8.29'
    configFile = file('config/checkstyle/checkstyle.xml')
}

spotbugs {
    excludeFilter = file('config/spotbugs/spotbugs-exclude.xml')
}

dependencyUpdates {
    // ignore alpha, beta, etc. versions
    rejectVersionIf { selection ->
        isNonStable(selection.candidate.version)
    }
}

// OWASP check (https://jeremylong.github.io/DependencyCheck/index.html)
dependencyCheck {
    failOnError = true
}

// run the full CS integration test suite
task csIntegrationTest(type: JavaExec) {
    main = 'com.imsweb.staging.cs.CsIntegrationTest'
    classpath = sourceSets.test.runtimeClasspath
}

// don't try to release a snapshot to a non-snapshot repository, that won't work anyway
if (version.endsWith('-SNAPSHOT')) {
    gradle.startParameter.excludedTaskNames += 'signMavenJavaPublication'
    gradle.startParameter.excludedTaskNames += 'closeAndReleaseRepository'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'staging-algorithm-cs'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'CS Staging Algorithm'
                description = 'Collaborative Staging algorithm for the staging-client-java'
                url = 'https://github.com/imsweb/staging-algorithm-cs'
                inceptionYear = '2015'

                licenses {
                    license {
                        name = 'A modified BSD License (BSD)'
                        url = 'https://github.com/imsweb/staging-algorithm-cs/blob/master/LICENSE'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'ctmay4'
                        name = 'Chuck May'
                        email = 'mayc@imsweb.com'
                    }
                }

                scm {
                    url = 'https://github.com/imsweb/staging-algorithm-cs'
                    connection = 'scm:https://github.com/imsweb/staging-algorithm-cs.git'
                    developerConnection = 'scm:git@github.com:imsweb/staging-algorithm-cs.git'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username = project.findProperty('nexusUsername') ?: ''
                password = project.findProperty('nexusPassword') ?: ''
            }
        }
    }
}

signing {
    def signingKey = project.findProperty('signingKey') ?: ''
    def signingPassword = project.findProperty('signingPassword') ?: ''

    useInMemoryPgpKeys(signingKey, signingPassword)

    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

// configure nexus staging plugin
nexusStaging {
    numberOfRetries = 50
    delayBetweenRetriesInMillis = 5000
}

wrapper {
    gradleVersion = '6.5'
    distributionType = Wrapper.DistributionType.ALL
}